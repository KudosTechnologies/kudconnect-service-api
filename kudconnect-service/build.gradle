import org.openapitools.generator.gradle.plugin.tasks.GenerateTask
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'org.jsonschema2pojo:jsonschema2pojo-gradle-plugin:1.1.2'
        classpath "org.openapitools:openapi-generator-gradle-plugin:$openApiGeneratorVersion"
    }
}
plugins {
    id 'org.springframework.boot' version '3.1.0'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'java'
    id 'idea'
    id 'jacoco'
}
apply plugin: "org.openapi.generator"
dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2022.0.3"
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    acceptanceImplementation {
        extendsFrom testImplementation
    }
    apiDef {
        transitive = false
    }
}


repositories {
    mavenLocal()
    maven { url "https://repo.spring.io/snapshot" }
    mavenCentral()
}

java {
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
}

def openApiDef = "$buildDir/dependencies/apiDef"
def openApiTemplateDir = "$buildDir/generated/openapi/templates".toString()
def openApiClientDir = "$buildDir/generated/openapi/client".toString()
def openApiServerDir = "$buildDir/generated/openapi/server".toString()
sourceSets {
    main {
        java.srcDir "src/main/java"
        java.srcDir "${openApiServerDir}/src/main/java"
        resources.srcDir "src/main/resources"
    }
    test {
        java.srcDir "src/test/java"
        resources.srcDir "src/test/resources"
    }
    acceptance {
        java.srcDir 'src/acceptance/java'
        java.srcDir "${openApiClientDir}/src/java"
        compileClasspath += sourceSets.main.output + configurations.testCompileClasspath + configurations.compileClasspath
        runtimeClasspath += output + compileClasspath
    }
}

idea {
    project {
        jdkName = "$javaVersion"
        languageLevel = "$javaVersion"
    }
    module {
        testSourceDirs += project.sourceSets.acceptance.java.srcDirs
        testSourceDirs += project.sourceSets.acceptance.resources.srcDirs
    }
}
dependencies {
    apiDef "ro.kudostech:kudconnect-service-api:$KudConnectApiVersion@yaml"
    implementation 'org.springframework.modulith:spring-modulith-bom:1.0.0-SNAPSHOT'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations', version: '2.2.12'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    implementation group: 'org.mapstruct', name: 'mapstruct', version: '1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    runtimeOnly 'org.postgresql:postgresql'

    // Test
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation(platform("org.junit:junit-bom:5.9.3"))
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine'
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation 'com.h2database:h2:2.1.214'

    // Acceptance tests
    acceptanceImplementation('org.springframework.boot:spring-boot-starter-test')
    acceptanceImplementation 'io.rest-assured:rest-assured'
    acceptanceImplementation 'io.cucumber:cucumber-java:7.12.1'
    acceptanceImplementation 'io.cucumber:cucumber-junit:7.12.1'
    acceptanceImplementation 'io.cucumber:cucumber-spring:7.12.1'
    acceptanceImplementation 'org.awaitility:awaitility:4.2.0'
    acceptanceImplementation 'com.auth0:java-jwt:4.4.0'
    acceptanceImplementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    acceptanceImplementation 'javax.validation:validation-api:2.0.1.Final'
    acceptanceImplementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    acceptanceImplementation group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: '5.2.1'
}
processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}
processTestResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}
processAcceptanceResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}
tasks.named("sourcesJar") { duplicatesStrategy = 'include' }
test {
    useJUnitPlatform()
    description = "Runs all unit tests definied via JUnit."
    group = "testing"
}
tasks.withType(Test) {
    testLogging.showStandardStreams = true
}
task acceptanceTest(type: Test) {
    description = "Runs the acceptance tests defined via Cucumber"
    group = "testing"
    testClassesDirs = sourceSets.acceptance.output.classesDirs
    classpath = sourceSets.acceptance.runtimeClasspath
    finalizedBy "jacocoAcceptanceTestReport"
}
build.dependsOn(acceptanceTest)
acceptanceTest.mustRunAfter(test)
jacoco {
    toolVersion = "0.8.10"
}
tasks.withType(JacocoReport) {
    reports {
        xml.required = true
        html.required = true
    }
}
jacocoTestReport {
    description = "Generates jacoco code coverage report for unit test tasks."
    group = "reporting"
    executionData test
    reports {
        xml.required = true
        html.required = true
    }
}
test.finalizedBy jacocoTestReport
task jacocoAcceptanceTestReport(type: JacocoReport) {
    description = "Generates jacoco code coverage report for acceptance test tasks."
    group = "reporting"
    executionData acceptanceTest
    sourceSets sourceSets.main
    reports {
        xml.required = true
        html.required = true
    }
}
acceptanceTest.finalizedBy jacocoAcceptanceTestReport
task allTestReport(type: JacocoReport) {
    description = "Generates jacoco code coverage report for unit, integration und acceptance test tasks."
    group = "reporting"
    executionData test, acceptanceTest
    sourceSets sourceSets.main
    reports {
        xml.required = true
        html.required = true
    }
}
task copyRuntimeLibs(type: Copy) {
    into "libs"
    from configurations.runtimeClasspath
    doLast {
        def files = fileTree("libs").filter { it.isFile() }.files.name
        files.each { def file ->
            exec {
                workingDir "libs"
                commandLine 'touch', '-a', '-m', '--date=@0', file
            }
        }
    }
}
tasks.register('copyOpenApiDef', Copy) {
    dependsOn configurations.apiDef
    delete "spec/external/"
    from configurations.apiDef.singleFile.absolutePath

    include "**/*.yaml", "**/*.yml"
    rename("kudconnect-service-api-" + project.property("KudConnectApiVersion") + ".yaml", "kudconnect-service-api.yaml")
    includeEmptyDirs = false
    into "$projectDir/spec/external/"
}
tasks.register('patchOpenApiTemplates') {
    group 'openapi tools'
    copy {
        from buildscript.configurations.classpath
        include "openapi-generator-${openApiGeneratorVersion}.jar"
        into openApiTemplateDir
    }
    copy {
        from(zipTree("$openApiTemplateDir/openapi-generator-${openApiGeneratorVersion}.jar")) {
            include "Java/*Enum.mustache"
            include "JavaSpring/enum*.mustache"
            include "JavaSpring/api.mustache"
        }
        into openApiTemplateDir
        filter {
            it
            // solve Jackson's issue with deserializing enums: https://github.com/FasterXML/jackson-module-kotlin/issues/336
                    .replace('@JsonCreator', '@JsonCreator(mode = JsonCreator.Mode.DELEGATING)')
        }
    }
}
task buildJavaClient(type: GenerateTask) {
    group 'openapi tools'
    generatorName = "java"
    inputSpec = "$projectDir/spec/external/kudconnect-service-api.yaml".toString()
    outputDir = openApiClientDir
    apiPackage = 'ro.kudostech.kudconnect.api.client'
    modelPackage = 'ro.kudostech.kudconnect.api.client.model'
    modelNameSuffix = 'Dto'
    templateDir = "${openApiTemplateDir}/Java"
    configOptions = [
            artifactDescription    : "generated organizationApi",
            artifactId             : "organizationApi",
            artifactUrl            : "https://bitbucket.interhyp.de/bitbucket/projects/BACKEND/repos/organization-api/",
            hideGenerationTimestamp: "true",
            library                : "resttemplate",
            openApiNullable        : "false",
            serializableModel      : "true",
            sourceFolder           : "src/java",
            useBeanValidation      : "true",
    ]
}
buildJavaClient.dependsOn copyOpenApiDef
buildJavaClient.dependsOn patchOpenApiTemplates
compileAcceptanceJava.dependsOn buildJavaClient

task buildJavaServer(type: GenerateTask) {
    group 'openapi tools'
    generatorName = "spring"
    inputSpec = "$projectDir/spec/external/kudconnect-service-api.yaml".toString()
    outputDir = openApiServerDir
    apiPackage = 'ro.kudostech.kudconnect.api.server'
    modelPackage = 'ro.kudostech.kudconnect.api.server.model'
    modelNameSuffix = 'Dto'

    configOptions = [
            dateLibrary                   : "java8",
            delegatePattern               : "false",
            enumPropertyNaming            : "UPPERCASE",
            exceptionHandler              : "false",
            generateBuilders              : "false",
            gradleBuildFile               : "false",
            hideGenerationTimestamp       : "true",
            interfaceOnly                 : "true",
            interfacePrefix               : "false",
//            library                       : "spring-cloud",
            library                       : "spring-boot",
            openApiNullable               : "false",
            removeEnumValuePrefix         : "false",
            removeOperationIdPrefix       : "true",
            skipDefaultInterface          : "true",
            useSpringBoot3                : "true",
            useBeanValidation             : "true",
            useTags                       : "true",
            // @lombok.Generated makes jacoco ignore the generated classes. The @Generated annotation added by openapi does not have the correct retention
            // period for this. See also https://www.baeldung.com/jacoco-report-exclude
            additionalModelTypeAnnotations: "@lombok.Generated",
            additionalEnumTypeAnnotations : "@lombok.Generated",
    ]

}
buildJavaServer.dependsOn copyOpenApiDef
buildJavaServer.dependsOn patchOpenApiTemplates
compileJava.dependsOn buildJavaServer

